-- A program is a 0 or more imports followed by 
-- zero or more function declarations
PImports.   Program     ::= Import ";" Program ;
PFuncs.     Program1    ::= Func Program1 ;
PLast.      Program1    ::= Func ;
coercions   Program 1 ;

-- A declaration is a list of function definitions
DFunc.      Func        ::= "function" Id ":" Types ";" [Def] ;
DImport.    Import      ::= "import" Id ;
terminator  nonempty Def ";" ;

-- Function definition
DDef.       Def         ::= Id [Arg] ":=" Exp ;
separator   Arg "" ;

-- Arguments can be either a literal, identifier or a list pattern
DArg1.      Arg         ::= Literal ;
DArg2.      Arg         ::= Id ;
DArg3.      Arg         ::= ListPattern ;

-- Pattern matching for lists
LPattern1.  ListPattern ::= "[]";
LPattern2.  ListPattern ::= "(" Id ":" Pattern ")";
LPattern3.  Pattern     ::= "[]" ;
LPattern4.  Pattern     ::= Id ;
LPattern5.  Pattern     ::= Id ":" Pattern ;


-- Predefined token types
LitInt.     Literal     ::= Integer ;
LitDouble.  Literal     ::= Double ;
LitChar.    Literal     ::= Char ;
LitString.  Literal     ::= [String] ;
separator nonempty String "" ;

-- Tailor-made token type
token Id (lower (letter | digit | '_' | '\'')*) ;

-- Expression types
EVar.       Exp4        ::= Id ;
ELiteral.   Exp4        ::= Literal ;
EApp.       Exp3        ::= Exp3 Exp4 ;
EAdd.       Exp2        ::= Exp2 "+" Exp3 ;
ESub.       Exp2        ::= Exp2 "-" Exp3 ;
ELt.        Exp1        ::= Exp1 "<" Exp2 ;
EGt.        Exp1        ::= Exp1 ">" Exp2 ;
ELtEq.      Exp1        ::= Exp1 "<=" Exp2 ;
EGtWq.      Exp1        ::= Exp1 ">=" Exp2 ;
EAbs.       Exp         ::= "\\" Id "->" Exp ;
coercions Exp 4 ;

-- Single or multiple argument type declarations
SType.      Types       ::= Type ;
AType.      Types       ::= Type "->" Types ;

-- Standard types
TBool.      Type        ::= "Bool" ;
TInt.       Type        ::= "Int" ;
TDouble.    Type        ::= "Double" ;
TChar.      Type        ::= "Char" ;
TString.    Type        ::= "String" ;

-- Haskell-style comments for now
comment "--" ;
comment "{-" "-}" ;
